/*--------------------------------------------
 * HmChatGPTWriting v1.0.6.1
 * 
 * Copyright (C) 2023 Akitsugu Komiyama
 * under the MIT License
 * 
 * (※秀丸エディタ v8.98以上)
 --------------------------------------------*/

// OPENAIのAPIのKEYの設定 
SET_OPENAI_KEY:

    $OPENAI_KEY = getenv("OPENAI_KEY");

    if ($OPENAI_KEY == "") {
        $OPENAI_KEY = ""; // 直接ここでAPIのKEYの文字列を指定しても良いですが、あまり推奨はしません。直書きする場合、このマクロを迂闊に配布して他者にAPIのキーが漏れないよう注意。
    }

SET_MODEL:

    $MODEL = "gpt-3.5-turbo"; // あなたのOpenAIアカウント内のAPIで、gpt-4 以上が使えるならば、"gpt-4"などの設定可能。しかしながら値段が10倍以上に跳ね上がるので注意。
                              // 最大トークンを大きくしたいなら gpt-3.5-turbo-16k などとすれば4倍になる。この場合は料金は2倍ほどで済むので格安具合をある程度維持できる。


SET_MAXTOKENS:

    #MAXTOKENS = 2000;    // この数値は概ね 使用対象のmodelの最大トークンの半分程度を使用するのが望ましい。
                          // 「gpt-3.5-turbo なら 2000」、「gpt-3.5-turbo-16k なら 8000」、「gpt-4 なら 4000」、「gpt-4-32k なら 16000」といった具合。


SAVE_SELECTED_TEXT:

    if (selecting) {
        $HmSelectedText = gettext2( seltopcolumn, seltoplineno, selendcolumn, selendlineno, 1 );
    }

    if ($HmSelectedText == "") {
        call OUTPUTPANE_WRITELINE "選択対象がありません";
    }


SHOW_MENU_AND_SELECT:

    if (selecting) {
        call ADD_MENUITEM
            "(&T)タイトル考案 ⇐ 選択中の複数の単語", 
            "以下のキーワードを基に日本語でタイトルを９個提案してください\n###\n" + $HmSelectedText;

        call ADD_MENUITEM
            "(&O)アウトライン案 ⇐ 選択中の単語群もしくはタイトル",
            "以下のキーワードを基に記事構成案を考えてください\n###\n" + $HmSelectedText;

        call ADD_MENUITEM
            "\x01",
            "";

        call ADD_MENUITEM
            "(&S)文面タタキ案 ⇐ 選択中のアウトラインの一部",
            "以下の構成内容に関して、それぞれに具体的内容の文章を考えてください\n###\n" + $HmSelectedText;

        call ADD_MENUITEM
            "\x01",
            "";
        call ADD_MENUITEM
            "(&E)簡潔文章案 ⇐ 選択中の文章",
            "以下の文章の全てを簡潔な文章に直して下さい。\n###\n" + $HmSelectedText;

        call ADD_MENUITEM
            "(&Y)超要約案 ⇐ 選択中の文章",
            "以下の文章全体を出来るだけ短い文章で要約してください。\n###\n" + $HmSelectedText;

        call ADD_MENUITEM
            "(&W)ツイート長 ⇐ 選択中の文章",
            "以下の文章をツイッターのツイートに収まる100文字以上140文字未満の文章に要約してください。\n###\n" + $HmSelectedText;

        call ADD_MENUITEM
            "(&D)詳細文章案 ⇐ 選択中の文章",
            "以下の文章の全てを深い内容の詳細な文章に直して下さい。\n###\n" + $HmSelectedText;

        call ADD_MENUITEM
            "(&C)変更文章案 ⇐ 選択中の文章",
            "以下の文章を文章構成を大きく変更して書き換えてください。３つ提示してください。\n###\n" + $HmSelectedText;

        call ADD_MENUITEM
            "\x01",
            "";

        call ADD_MENUITEM
            "(&K)根拠やデータ ⇐ 選択中の文章",
            "以下の文章の根拠やデータを提示してください、必要に応じて「表」や「アスキーアートを使ったグラフ」を使って下さい。\n###\n" + $HmSelectedText;

        call ADD_MENUITEM
            "\x01",
            "";

        call ADD_MENUITEM
            "(&I)言葉の意味 ⇐ 選択中の単語",
            "以下の言葉の意味を説明してください\n###\n" + $HmSelectedText + "\n###\n" + 
            "# 条件\n" +
            "・架空の言葉である場合を考慮してください。\n" +
            "・回答に自身がない場合は答えないでください。\n" +
            "・不正確な可能性が高い場合は答えないでください。\n" +
            "・理解できなければ答えないでください。\n";

        call ADD_MENUITEM
            "(&R)類似表現 ⇐ 選択中の単語",
            "以下の単語の類似表現を９個提案してください\n###\n" + $HmSelectedText;

        call ADD_MENUITEM
            "(&M)誤字脱字 ⇐ 選択中の文章",
            "以下の文章の全てに対して誤字脱字や、タイプミス、不適切な表現をチェックしてください。\n###\n" + $HmSelectedText;

        call ADD_MENUITEM
            "\x01",
            "";

        call ADD_MENUITEM
            "(&U)Youtube用脚本案 ⇐ 選択中の単語群もしくはタイトル",
            "Youtube用に「" + $HmSelectedText + "」についての動画を作成するから脚本を書いてください。";

        call ADD_MENUITEM
            "(&L)詳細台本案 ⇐ 選択中の脚本の一部分",
            "以下の部分の詳細な台本を書いて下さい。\n###\n" + $HmSelectedText;

        call ADD_MENUITEM
            "\x01",
            "";

        call ADD_MENUITEM
            "(&H)履歴の削除 ⇐ (AIリセット＆消費トークンの節約)",
            "HmChatGPTWriting:ClearConversationChatGPT"; // 勝手に文字列変更しないこと。

        // メニューの選択
        call SELECT_MENU;
        #selected_item = ##return;
        if (#selected_item <= 0) {
            endmacro;
        }

        if (strstr($EXPRESSION[#selected_item-1], "HmChatGPTWriting:") < 0) {
            // 選択を解除して、カーソル位置を次の行に整える。
            call REPOSITION_CURSOR;
        }

    } else {

        call ADD_MENUITEM
            "(&H)履歴の削除 ⇐ (AIリセット＆消費トークンの節約)",
            "HmChatGPTWriting:ClearConversationChatGPT"; // 勝手に文字列変更しないこと。

        // メニューの選択
        call SELECT_MENU;
        #selected_item = ##return;
        if (#selected_item <= 0) {
            endmacro;
        }
    }


// AIに分析してもらう文字列
SETTING_ANALYSISTEXT:
    // この$HmTargetAnalyzeTextという変数名は固定なので変更しないこと。
    $HmTargetAnalyzeText = $EXPRESSION[#selected_item-1];


// 他の秀丸プロセスでHmOpenAIGPTWritingが使われているかどうか
CHECK_OPENAI_USED_HIDEMARUHANDLE:

    // HmOpenAIGPTWriting のウィンドウを表示中
    if (findwindow("*-- HmChatGPTWriting --*")) {

        // ChatGPTにリクエスト中ですが、上書きで問い合わせします。
        call OUTPUTPANE_WRITELINE "ChatGPTにリクエスト中ですが、問い合わせを上書きします。";

    }

    call SHOW_OPENAI_FORM;

    endmacro;


// メニューに項目を足し込んでいく
ADD_MENUITEM:
    $ITEM_ARRAY[#MENUITEM_IX] = $$1;
    $EXPRESSION[#MENUITEM_IX] = $$2;

    #MENUITEM_IX = #MENUITEM_IX + 1;

    return;


// メニューを表示し、選択番号を返す。
SELECT_MENU:

    #ITEM_CNT = 0;
    while(true) {
        if ($ITEM_ARRAY[#ITEM_CNT] == "") {
            break;
        }
        #ITEM_CNT = #ITEM_CNT + 1;
    }
    menuarray $ITEM_ARRAY, #ITEM_CNT;

    return result;


// 選択を解除し、カーソル位置を整える。
REPOSITION_CURSOR:
    begingroupundo;

    // 選択範囲の終端が先頭意外なら、その行の一部を選択している。
    if (selendcolumn != 0) {
        ##column_ontheway = 1;
    } else {
        ##column_ontheway = 0;
    }
    moveto2 selendcolumn, selendlineno;
    escape;
    if (##column_ontheway) {
        golineend;
    }
    insertreturn;
    insertreturn;
    endgroupundo;
    return;


// ChatGPTのThinkingフォームの表示
SHOW_OPENAI_FORM:

    #CHATGPT_OBJ = createobject( currentmacrodirectory + @"\HmChatGPTWriting.comhost.dll", "{0D346374-D9DF-4095-A237-05ECD19A9E42}");

    // 秀丸の該当プロセスが終了するまでオブジェクトは維持
    keepobject #CHATGPT_OBJ, 1;

    // 秀丸プロセスが閉じる際に(エラーで閉じる際も含めて)、このメソッドを実行する
    setcomdetachmethod #CHATGPT_OBJ, "DestroyForm";

    // フォームの表示。
    #_ = member(#CHATGPT_OBJ, "CreateForm", $OPENAI_KEY, $MODEL, #MAXTOKENS);

    return;


// アウトプット枠への表示
OUTPUTPANE_WRITELINE:
    $$msg = $$1;

    #OUTPUTPANE = loaddll("HmOutputPane.dll");
    #ret=dllfunc(#OUTPUTPANE, "Output", hidemaruhandle(0), $$msg + "\r\n");

    return;