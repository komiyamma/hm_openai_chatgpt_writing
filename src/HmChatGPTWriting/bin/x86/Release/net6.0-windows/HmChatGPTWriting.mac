/*--------------------------------------------
 * HmChatGPTWriting v1.0.5
 * 
 * Copyright (C) 2023 Akitsugu Komiyama
 * under the MIT License
 * 
 * (※秀丸エディタ v8.98以上)
 --------------------------------------------*/

// OPENAIのAPIのKEYの設定 
SET_OPENAI_KEY:

    $OPENAI_KEY = getenv("OPENAI_KEY");

    if ($OPENAI_KEY == "") {
        $OPENAI_KEY = ""; // 直接ここでAPIのKEYの文字列を指定しても良いですが、あまり推奨はしません。直書きする場合、このマクロを迂闊に配布して他者にAPIのキーが漏れないよう注意。
    }

SET_MODEL:
    $MODEL = "gpt-3.5-turbo"; // あなたのOpenAIアカウント内のAPIで、gpt-4 以上が使えるならば、"gpt-4"などの設定可能。しかしながら値段が10倍以上に跳ね上がるので注意。

SAVE_SELECTED_TEXT:

    if (selecting) {
        $HmSelectedText = gettext2( seltopcolumn, seltoplineno, selendcolumn, selendlineno, 1 );
    }

    if ($HmSelectedText == "") {
        call OUTPUTPANE_WRITELINE "選択対象がありません";
    }

SHOW_MENU_AND_SELECT:
    if (selecting) {
        call ADD_MENUITEM
            "(&T)タイトル考案 ⇐ 選択中の複数の単語", 
            "以下のキーワードを基にタイトルを９個提案してください\n\n" + $HmSelectedText;

        call ADD_MENUITEM
            "(&O)記事構成案 ⇐ 選択中の単語群もしくはタイトル",
            "以下のキーワードを基に記事構成案を考えてください\n\n" + $HmSelectedText;

        call ADD_MENUITEM
            "\x01",
            "";

        call ADD_MENUITEM
            "(&S)構成内容の下書き ⇐ 選択中の構成ツリー",
            "以下の構成内容に当てはめて、それぞれの項目について下書きの文章を考えてください\n\n" + $HmSelectedText;

        call ADD_MENUITEM
            "(&E)簡潔文章案 ⇐ 選択中の文章",
            "以下の文章の全てを簡潔な文章に直して下さい。\n\n" + $HmSelectedText;

        call ADD_MENUITEM
            "(&Y)超要約案 ⇐ 選択中の文章全体",
            "以下の文章全体を出来るだけ短い文章で要約してください。\n\n" + $HmSelectedText;

        call ADD_MENUITEM
            "(&D)詳細文章案 ⇐ 選択中の文章",
            "以下の文章の全てを深い内容の詳細な文章に直して下さい。\n\n" + $HmSelectedText;

        call ADD_MENUITEM
            "(&C)変更文章案 ⇐ 選択中の文章",
            "以下の文章を文章構成を大きく変更して書き換えてください。３つ提示してください。\n\n" + $HmSelectedText;

        call ADD_MENUITEM
            "\x01",
            "";

        call ADD_MENUITEM
            "(&S)類似表現 ⇐ 選択中の単語",
            "以下の単語の類似表現を９個提案してください\n\n" + $HmSelectedText;

        call ADD_MENUITEM
            "(&M)誤字脱字 ⇐ 選択中の文章",
            "以下の文章の全てに対して誤字脱字や、不適切な表現をチェックしてください。\n\n" + $HmSelectedText;

        call ADD_MENUITEM
            "\x01",
            "";

        call ADD_MENUITEM
            "(&H)履歴の削除 ⇐ (AIリセット＆消費トークンの節約)",
            "HmChatGPTWriting:ClearConversationChatGPT"; // 勝手に文字列変更しないこと。

        // メニューの選択
        call SELECT_MENU;
        #selected_item = ##return;
        if (#selected_item <= 0) {
            endmacro;
        }

        // if (strstr($EXPRESSION[#selected_item], "HmChatGPTWriting:") < 0) {
            // 選択を解除して、カーソル位置を次の行に整える。
            call REPOSITION_CURSOR;
        // }

    } else {

        call ADD_MENUITEM
            "(&H)履歴の削除 ⇐ (AIリセット＆消費トークンの節約)",
            "HmChatGPTWriting:ClearConversationChatGPT"; // 勝手に文字列変更しないこと。

        // メニューの選択
        call SELECT_MENU;
        #selected_item = ##return;
        if (#selected_item <= 0) {
            endmacro;
        }
    }


// AIに分析してもらう文字列
SETTING_ANALYSISTEXT:
    // この$HmTargetAnalyzeTextという変数名は固定なので変更しないこと。
    $HmTargetAnalyzeText = $EXPRESSION[#selected_item-1];


// 他の秀丸プロセスでHmOpenAIGPTWritingが使われているかどうか
CHECK_OPENAI_USED_HIDEMARUHANDLE:

    // HmOpenAIGPTWriting のウィンドウを表示中
    if (findwindow("*-- HmChatGPTWriting --*")) {

        // ChatGPTにリクエスト中ですが、上書きで問い合わせします。
        call OUTPUTPANE_WRITELINE "ChatGPTにリクエスト中ですが、問い合わせを上書きします。";

    }

    call SHOW_OPENAI_FORM;

    endmacro;


// メニューに項目を足し込んでいく
ADD_MENUITEM:
    $ITEM_ARRAY[#MENUITEM_IX] = $$1;
    $EXPRESSION[#MENUITEM_IX] = $$2;

    #MENUITEM_IX = #MENUITEM_IX + 1;

    return;


// メニューを表示し、選択番号を返す。
SELECT_MENU:

    #ITEM_CNT = 0;
    while(true) {
        if ($ITEM_ARRAY[#ITEM_CNT] == "") {
            break;
        }
        #ITEM_CNT = #ITEM_CNT + 1;
    }
    menuarray $ITEM_ARRAY, #ITEM_CNT;

    return result;


// 選択を解除し、カーソル位置を整える。
REPOSITION_CURSOR:
    begingroupundo;

    // 選択範囲の終端が先頭意外なら、その行の一部を選択している。
    if (selendcolumn != 0) {
        ##column_ontheway = 1;
    } else {
        ##column_ontheway = 0;
    }
    moveto2 selendcolumn, selendlineno;
    escape;
    if (##column_ontheway) {
        golineend;
    }
    insertreturn;
    insertreturn;
    endgroupundo;
    return;


// ChatGPTのThinkingフォームの表示
SHOW_OPENAI_FORM:

    #CHATGPT_OBJ = createobject( currentmacrodirectory + @"\HmChatGPTWriting.comhost.dll", "{0D346374-D9DF-4095-A237-05ECD19A9E42}");

    // 秀丸の該当プロセスが終了するまでオブジェクトは維持
    keepobject #CHATGPT_OBJ, 1;

    // 秀丸プロセスが閉じる際に(エラーで閉じる際も含めて)、このメソッドを実行する
    setcomdetachmethod #CHATGPT_OBJ, "DestroyForm";

    // フォームの表示。
    #_ = member(#CHATGPT_OBJ, "CreateForm", $OPENAI_KEY, $MODEL);

    return;


// アウトプット枠への表示
OUTPUTPANE_WRITELINE:
    $$msg = $$1;

    #OUTPUTPANE = loaddll("HmOutputPane.dll");
    #ret=dllfunc(#OUTPUTPANE, "Output", hidemaruhandle(0), $$msg + "\r\n");

    return;